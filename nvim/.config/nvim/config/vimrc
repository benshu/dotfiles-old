"      .-.     .-.     .-.     .-.     .-.     .-.     .-.
" `._.'   `._.'   `._.'   `._.'   `._.'   `._.'   `._.'
"
" https://github.com/rafi/vim-config

" Runtime and Plugins
" -------------------------------------------------

if &compatible
	set nocompatible
endif

" Set main configuration directory, and where cache is stored.
let $VIMPATH = fnamemodify(resolve(expand('<sfile>:p')), ':h:h')
let $VARPATH = expand(($XDG_CACHE_HOME ? $XDG_CACHE_HOME : '~/.cache').'/vim')

let g:dein#install_max_processes = 16
let g:dein#install_progress_type = 'echo'
let g:dein#enable_notification = 1
let g:dein#install_log_filename = $VARPATH.'/dein.log'

function! s:dein_load_yaml(filename) abort
	let g:denite_plugins = json_decode(
				\ system('yaml2json', readfile(a:filename)))

	for plugin in g:denite_plugins
		call dein#add(plugin['repo'], extend(plugin, {}, 'keep'))
	endfor
	unlet g:denite_plugins
endfunction

function! s:source_file(path, ...) abort
	let use_global = get(a:000, 0, ! has('vim_starting'))
	let abspath = resolve(expand($VIMPATH.'/config/'.a:path))
	if ! use_global
		execute 'source' fnameescape(abspath)
		return
	endif

	let content = map(readfile(abspath),
		\ "substitute(v:val, '^\\W*\\zsset\\ze\\W', 'setglobal', '')")
	let tempfile = tempname()
	try
		call writefile(content, tempfile)
		execute printf('source %s', fnameescape(tempfile))
	finally
		if filereadable(tempfile)
			call delete(tempfile)
		endif
	endtry
endfunction

" Set augroup
augroup MyAutoCmd
	autocmd!
	autocmd CursorHold *? syntax sync minlines=300
augroup END

" Initialize base requirements
if has('vim_starting')
	" Vim Initialization
	" ------------------

	" Global Mappings "{{{
	" Use spacebar as leader and ; as secondary-leader
	" Required before loading plugins!
	let g:mapleader="\<Space>"
	let g:maplocalleader=','

	" }}}
	" Ensure cache directory "{{{
	if ! isdirectory(expand($VARPATH))
		" Create missing dirs i.e. cache/{undo,backup}
		call mkdir(expand('$VARPATH/undo'), 'p')
		call mkdir(expand('$VARPATH/backup'))
	endif

	" Ensure custom spelling directory
	if ! isdirectory(expand('$VIMPATH/spell'))
		call mkdir(expand('$VIMPATH/spell'))
	endif
	" }}}
	" Setup dein {{{
	if &runtimepath !~# '/dein.vim'
		let s:dein_dir = expand('$VARPATH/dein').'/repos/github.com/Shougo/dein.vim'
		if ! isdirectory(s:dein_dir)
			execute '!git clone https://github.com/Shougo/dein.vim' s:dein_dir
		endif

		execute 'set runtimepath+='.substitute(
			\ fnamemodify(s:dein_dir, ':p') , '/$', '', '')
	endif
	" }}}

	" Write history on idle, for sharing among different sessions
	autocmd MyAutoCmd CursorHold * if exists(':rshada') | rshada | wshada | endif

	" Search and use environments specifically made for Neovim.
	if isdirectory($VARPATH.'/venv/neovim2')
		let g:python_host_prog = $VARPATH.'/venv/neovim2/bin/python'
	endif
	if isdirectory($VARPATH.'/venv/neovim3')
		let g:python3_host_prog = $VARPATH.'/venv/neovim3/bin/python'
	endif
endif

" Initialize dein.vim (package manager)
let s:path = expand('$VARPATH/dein')
let s:plugins_path = expand('$VIMPATH/config/plugins.yaml')
let s:user_plugins_path = expand('$VIMPATH/config/local.plugins.yaml')
if dein#load_state(s:path)
	call dein#begin(s:path, [expand('<sfile>'), s:plugins_path])
	try
		call s:dein_load_yaml(s:plugins_path)
		if filereadable(s:user_plugins_path)
			call s:dein_load_yaml(s:user_plugins_path)
		endif
	catch /.*/
		echoerr v:exception
		echomsg 'Error loading config/plugins.yaml...'
		echomsg 'Caught: ' v:exception
		echoerr 'Please run: pip3 install --user PyYAML'
	endtry

	if isdirectory(expand('$VIMPATH/dev'))
		call dein#local(expand('$VIMPATH/dev'), {'frozen': 1, 'merged': 0})
	endif
	call dein#end()
	call dein#save_state()
	if dein#check_install()
		if ! has('nvim')
			set nomore
		endif
		call dein#install()
	endif
endif

call s:source_file('plugins/all.vim')

filetype plugin indent on
syntax enable

if ! has('vim_starting')
	call dein#call_hook('source')
	call dein#call_hook('post_source')
endif

" Loading configuration modules
call s:source_file('general.vim')
call s:source_file('filetype.vim')
call s:source_file('mappings.vim')
call s:source_file('theme.vim')

" Load user custom local settings
if filereadable(expand('$VIMPATH/config/local.vim'))
	call s:source_file('local.vim')
endif
set secure

" vim: set foldmethod=marker ts=2 sw=2 tw=80 noet :
